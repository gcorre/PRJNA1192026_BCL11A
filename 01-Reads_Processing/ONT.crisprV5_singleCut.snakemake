## CRISPR long read alignement V5 for locus with a single cut (where no large deletion is expected)
## july 2024
## CORRE GUILLAUME @ GENETHON


# align read on the genome 
# realign read on target to amplicon + plasmide

## snakemake -s ../../ONT.crispr -k -j 48 --use-conda --conda-prefix ~/conda-envs --conda-frontend mamba

import os;
import re;
from glob import glob;

libraries = glob("*.fastq.gz")
libraries = [re.sub(".fastq.gz", '', w) for w in libraries];

configfile: "./ont.configV5_singleCut.yaml"

############################ START #######################################################

rule all:
    input: 
     expand("../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.annotated.bed", library = libraries),
     expand("../analysis/{library}/{library}.QC.log", library = libraries),
     expand("../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.bam.cov", library = libraries),
     expand("../analysis/{library}/{library}_summary.txt", library = libraries),
     expand("../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.sniffles.vcf", library = libraries),
     expand("../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.composition", library = libraries)


## FILTER and TRIM READS
rule trimQuality:
    input: "{library}.fastq.gz"
    output:"../analysis/{library}/{library}.QCfiltered.fastq.gz"
    threads: 6
    conda: "tools"
    log : "../analysis/{library}/{library}.QCfiltered.log"
    params: minSize=config["min_read_size"], quality=config["min_quality_5'_3'"]
    shell: """
            cutadapt -j {threads} -q {params.quality} -m {params.minSize} {input} -o {output} > {log}
            """

rule trimAdaptor:
    input: rules.trimQuality.output
    output: "../analysis/{library}/{library}.trimmed.fastq.gz"
    threads: 6
    log: "../analysis/{library}/{library}.trimmed.log"
    conda: "tools"
    shell: """
        porechop -i {input} -o {output} --extra_end_trim 0  --extra_middle_trim_good_side 0 --extra_middle_trim_bad_side 0  --min_trim_size 10 -t {threads}  > {log}
        """





## Index genome, Align reads , sort reads and index bam
rule index_genome_mm2:
    input: config["reference_genome"]
    output: config["mm2_index"]
    conda: "tools"
    threads: 12
    shell: """
            minimap2 -t {threads} -d {output} -x map-ont  {input}
        """

rule align_genome_mm2:
    input: fq=rules.trimAdaptor.output, idx=rules.index_genome_mm2.output
    output: temp("../analysis/{library}/genome/{library}.trimmed.GenomeAligned.sam")
    threads: 6
    conda: "tools"
    message: "Align reads on the reference genome"
    shell: """
        minimap2 -t {threads}  -x map-ont  -a -Y  --secondary=no  -o {output} {input.idx}  {input.fq};
        """ 

rule sort_genome_aligned: 
    input: al=rules.align_genome_mm2.output, genome=config["reference_genome"]
    output: bam="../analysis/{library}/genome/{library}.trimmed.GenomeAligned.sorted.bam"
    threads: 6
    conda: "tools"
    message: "Remove unmapped reads and sort bam"
    shell: """
        samtools view -h -F 4  {input.al} | samtools sort - | samtools calmd -b - {input.genome} > {output.bam}  ;
        samtools index {output.bam}
        """



## filter reads that are on-target, even partialy and save as fastq
rule get_OT_reads:
    input: rules.sort_genome_aligned.output.bam
    output: ot_RNAMES=temp("../analysis/{library}/genome/{library}.trimmed.GenomeAligned.sorted.OT.txt"),
     tmp=temp("../analysis/{library}/genome/{library}.tmp.txt"),
     ot="../analysis/{library}/genome/{library}.trimmed.GenomeAligned.sorted.OT.fastq.gz"
    conda: "tools"
    threads: 1
    message: "Keep reads that overlap the enrichment region between the enrichment guides"
    params: chrom=config["gRNA"]["chromosome"], 
     left=config["gRNA"]["enrichment"]["left"]["cut_position"], 
     right=config["gRNA"]["enrichment"]["right"]["cut_position"]
    shell: """   
        samtools view  {input} {params.chrom}:{params.left}-{params.right} | cut -f1 | sort | uniq > {output.tmp};
        sort {output.tmp} | uniq -c | awk '$1>0 {{print $2}}'  > {output.ot_RNAMES};
        samtools view -hb {input} -N {output.ot_RNAMES} | samtools fastq - | gzip > {output.ot}
        """




################################################################################
#### Align reads on the ROI

## Use genbank file to extract fasta sequence and bed annotations
rule make_reference:
    input: 
    output: fa=os.path.splitext(config["reference_plasmid"])[0]+".fa", bed=os.path.splitext(config["reference_plasmid"])[0]+".bed"
    conda: "tools"
    params: scripts=config["scripts_folder"], plasmid=config["reference_plasmid"]
    shell: """
        if [[ -f "{params.plasmid}" ]]
        then
            plasmid_name=$(basename {params.plasmid})
            sed -i "s/Exported.*/$plasmid_name/" {params.plasmid}

            python {params.scripts}/genbank_to_bed.py {params.plasmid} {output.bed}
            python {params.scripts}/genbank_to_fa.py {params.plasmid} {output.fa}
        else
            echo ">empty" > {output.fa}
            echo "" > {output.bed}
        fi
        """

#### Align reads on the ROI

rule make_ROI_reference:
    input: genome=config["reference_genome"], plasmid=rules.make_reference.output.fa
    output: full_length="../misc/"+config["gRNA"]["locus"]+"_locus.fa"
    threads: 1
    conda: "tools"
    params: ref_name=config["gRNA"]["locus"],
     chr=config["gRNA"]["chromosome"],
     left=config["gRNA"]["enrichment"]["left"]["cut_position"], 
     right=config["gRNA"]["enrichment"]["right"]["cut_position"],
     plasmid=config["reference_plasmid"]
    shell: """

        ## FULL LENGTH ENRICHMENT REGION
        echo '>'{params.ref_name} > {output.full_length}
        samtools faidx  {input.genome} {params.chr}:{params.left}-{params.right} | awk 'NR>1' >> {output.full_length} ## add enrichment region sequence

        if [[ -f "{params.plasmid}" ]]
        then
            cat {input.plasmid} >> {output.full_length} ##add plasmid sequence
        fi
        """

rule index_ROI:
    input: full_length=rules.make_ROI_reference.output.full_length
    output: full_length="../misc/"+config["gRNA"]["locus"]+"_locus.mm2"
    threads: 6
    conda: "tools"
    message: "index enrichment region with adapted mm2 parameters to force split alignment at deletion"
    params: 
     size=config["mm2_size"],
     r=config["mm2_r"], 
     k=config["mm2_K"], 
     w=config["mm2_W"] 
    shell: """
        minimap2 -t {threads} -d {output.full_length} -x map-ont -I {params.size} -r {params.r} -k {params.k} -w {params.w} {input.full_length}
        """

rule align_locus_mm2:
    input: fq=rules.get_OT_reads.output.ot, idx=rules.index_ROI.output.full_length
    output: temp("../analysis/{library}/locus/{library}.locusAligned.sam")
    threads: 6
    conda: "tools"
    message: "Align reads on the enrichment region"
    params: r=config["mm2_r"], 
     k=config["mm2_K"],
     w=config["mm2_W"],
     size=config["mm2_size"]
    shell: """
        minimap2 -t {threads}   -x map-ont  -n 20 -I {params.size} -r {params.r} -k {params.k} -w {params.w} --split-prefix ../analysis/{wildcards.library}/split -a -Y  --secondary=no  -o {output} {input.idx}  {input.fq};
        """ 





rule sort_amplicon: 
    input: al=rules.align_locus_mm2.output, genome=rules.make_ROI_reference.output.full_length
    output: bam="../analysis/{library}/locus/{library}.locusAligned.sorted.bam", 
        tmp=temp("../analysis/{library}/locus/{library}.tmp.txt"),
        tmp2=temp("../analysis/{library}/locus/{library}.tmp2.txt"),
        end2end="../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.bam"
    threads: 6
    conda: "tools"
    message: "Keep reads that overlap the editing site"
    params: locus=config["gRNA"]["locus"],
     first_cut=config["gRNA"]["editing"]["left"]["cut_position"], 
     left=config["gRNA"]["enrichment"]["left"]["cut_position"], 
     right=config["gRNA"]["enrichment"]["right"]["cut_position"],
     offset=config["offset"],
     offset_del=config["offset_deletion"]
    shell: """
        
        ## CALCULATE COORDINATES RELATIVE TO ENRICHMENT GUIDES CUTTING SITES

        enrichment_relative_end=$(expr {params.right} - {params.left})
        first_cut_relative_pos=$(expr {params.first_cut} - {params.left} - {params.offset})
        second_cut_relative_pos=$(expr {params.first_cut} - {params.left} + {params.offset})

        align_left_of_first_cut=$(echo {params.locus}:1-$first_cut_relative_pos)
        align_right_of_second_cut=$(echo {params.locus}:$second_cut_relative_pos-$enrichment_relative_end)

        samtools view -h -F 4 -q 5 {input.al} | samtools sort - | samtools calmd -b - {input.genome} > {output.bam}  ;
        samtools index {output.bam}

        samtools view  {output.bam} $align_left_of_first_cut | cut -f1 | sort | uniq > {output.tmp};
        samtools view  {output.bam} $align_right_of_second_cut | cut -f1 | sort | uniq >> {output.tmp};
        sort {output.tmp} | uniq -c | awk '$1>1 {{print $2}}'  > {output.tmp2};
        samtools view -hb {output.bam} -N {output.tmp2} > {output.end2end}
        samtools index {output.end2end}
        """


rule make_annotation_locus:
    input:
    output: annotation="../misc/annotation.bed"
    threads: 1
    conda: 'tools'
    params: locus=config["gRNA"]["locus"],
     first_cut=config["gRNA"]["editing"]["left"]["cut_position"], 
     left=config["gRNA"]["enrichment"]["left"]["cut_position"], 
     right=config["gRNA"]["enrichment"]["right"]["cut_position"],
     offset=config["offset"]
    shell: """

        enrichment_relative_end=$(expr {params.right} - {params.left})
        first_cut_relative_pos=$(expr {params.first_cut} - {params.left} - {params.offset})
        second_cut_relative_pos=$(expr {params.first_cut} - {params.left} + {params.offset})
        echo -e {params.locus}"\t1\t$first_cut_relative_pos\tleft_part\t0\t+" > {output.annotation}
        echo -e {params.locus}"\t$second_cut_relative_pos\t$enrichment_relative_end\tright_part\t0\t+" >> {output.annotation}
    """

#################################################################
#################################################################
#################################################################



## Bam to Bed

rule bamTobed:
    input :locus=rules.sort_amplicon.output.end2end
    output : locus = "../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.bed"   ## all reads on locus
    conda: "tools"
    threads : 1
    shell: """
        bedtools bamtobed -i {input.locus} -cigar > {output.locus}
        """


## annotate reads fragments to plasmid or genome or amplicon features
rule annotate_read:
    input: locus=rules.bamTobed.output.locus, annotation=rules.make_annotation_locus.output.annotation,  plasmid=rules.make_reference.output.bed
    output: locus="../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.annotated.bed"
    threads: 1
    conda:"tools"
    params: 
     plasmid=config["reference_plasmid"]
    shell: """
        if [[ -f "{params.plasmid}" ]]
        then
            bedtools intersect -nonamecheck  -wao -a {input.locus}  -b {input.annotation} {input.plasmid} -names locus vector > {output.locus}
        else
            bedtools intersect -nonamecheck  -wao -a {input.locus}  -b {input.annotation} -names locus > {output.locus}
        fi
        """



rule library_qc:
    input: "{library}.fastq.gz", rules.trimAdaptor.output,rules.trimQuality.output,rules.get_OT_reads.output.ot
    output:"../analysis/{library}/{library}.QC.log"
    threads: 2
    conda: "tools"
    shell: """
            seqkit stats {input}  -abT -j {threads} -o {output}
            """

rule coverage:
    input : rules.sort_amplicon.output.end2end
    output : "../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.bam.cov"
    conda : "tools"
    shell : """
         samtools depth -l 500 -d 0 -Q 1  {input} > {output}
        """


rule SV_calling_sniffles:
    input: e2e=rules.sort_amplicon.output.end2end
    output: e2e="../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.sniffles.vcf"
    threads : 6
    conda: 'tools'
    threads: 6
    params: minReadsPerSV=1,
     MaxDistGroupSV=1,
     MinSvLength=4,
     MinReadFragLength=2000
    shell: """

        if [[ $(samtools view -c {input.e2e}) == 0 ]]; 
        then 
            touch {output.e2e} 
        else
            sniffles -t {threads} --tmp_file ../analysis/{wildcards.library}/locus/{wildcards.library} -n -1 -r {params.MinReadFragLength} -s {params.minReadsPerSV} -d {params.MaxDistGroupSV} -l {params.MinSvLength} -m {input.e2e} -v {output.e2e} || true
        fi
        """


rule SV_calling_cuteSV:
    input: e2e=rules.sort_amplicon.output.end2end
    output: e2e="../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.cuteSV.vcf"
    threads : 6
    conda: 'tools'
    threads: 6
    shell: """

        if [[ $(samtools view -c {input.e2e}) == 0 ]]; 
        then 
            touch {output.e2e} 
        else
            cuteSV --threads {threads} --report_readid -p -1 -md 0 -mi 0 -r 1000 -s 1 -l 4 --max_cluster_bias_INS 1 --max_cluster_bias_DEL 1 --max_cluster_bias_INV 1 --max_cluster_bias_TRA 1  {input.e2e} {input.ref_full}  {output.e2e} ../analysis/{wildcards.library}/locus/ || true
        fi
        """

rule base_composition:
    input: bam=rules.sort_amplicon.output.end2end, ref=rules.make_ROI_reference.output
    output: "../analysis/{library}/locus/{library}.locusAligned.sorted.end2end.composition"
    threads: 1
    conda: "tools"
    params: features_locus=config["gRNA"]["locus"]
    shell: """
         pysamstats -t variation -c {params.features_locus} --stepper=nofilter  -u  -D 100000000 -f {input.ref} {input.bam} > {output}
    """



## make a summary table 
rule summary:
    input : raw="{library}.fastq.gz",
     QC= rules.trimQuality.output,
     adaptors = rules.trimAdaptor.output,
     genome_all = rules.sort_genome_aligned.output.bam,
     genomeOT = rules.get_OT_reads.output.ot,
     ROI=rules.sort_amplicon.output.bam,
     ROIe2e=rules.sort_amplicon.output.end2end
    output : "../analysis/{library}/{library}_summary.txt"
    conda : "tools"
    threads : 1
    shell : """
        echo "# " {wildcards.library}> {output}
        echo "# " $(date) >> {output}
        echo "# " {config[gRNA][locus]}"_"{config[gRNA][editing][left][name]}"_"{config[gRNA][enrichment][left][cut_position]}:{config[gRNA][enrichment][right][cut_position]} >> {output}
        echo $(zcat {input.raw} | grep -c "^@") "raw reads" >> {output}
        echo "  "$(zcat {input.QC} | grep -c "^@") "QC pass reads" >> {output}
        echo "    "$(zcat {input.adaptors} | grep -c "^@") "trimmed reads" >> {output}
        echo "      "$(samtools view -F 2048 -c {input.genome_all}) "Genome aligned reads" >> {output}
        echo "        "$(samtools view -F 2048 -c {input.genomeOT}) "Genome OnTarget reads" >> {output}
        echo "          "$(samtools view -F 2048 -c {input.ROI}) "ROI aligned reads" >> {output}
        echo "            "$(samtools view -F 2048 -c {input.ROIe2e})  "ROI end to end aligned reads" >> {output}
        """